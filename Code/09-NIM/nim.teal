#pragma version 4
// Handle each possible OnCompletion type. We don't have to worry about
// handling ClearState, because the ClearStateProgram will execute in that
// case, not the ApprovalProgram.

txn OnCompletion
int NoOp
==
bnz handle_noop

txn OnCompletion
int OptIn
==
bnz handle_optin

txn OnCompletion
int CloseOut
==
bnz handle_closeout

txn OnCompletion
int UpdateApplication
==
bnz handle_updateapp

txn OnCompletion
int DeleteApplication
==
bnz handle_deleteapp

// Unexpected OnCompletion value. Should be unreachable.
err

handle_noop:
// Handle NoOp
// Check for creator
addr JNXSVKOM24BQWKFWOZVWNL24ONVQ5K7TP7YD6EJGU4JERFH5JMO6X35KKI
txn Sender
==
bnz SShandle_optin

byte "turn"
app_global_get
int 0
==
//Alice's addr
addr ROM5Z3ESKA3PX3YGHKEH3RSASSR4PKKBSTQFMGPKU4NOESI6UVPMS77TTY
txn Sender
==
&&
byte "turn"
app_global_get
int 1
==
//Bob's addr
addr 5EKIODPFSULLMQT4K4KMVAECRKSWXN2QL7BB7IG4OQLHE4DS6QMEXEQ4LY
txn Sender
==
&&
||
bz Finish

byte "heap"
app_global_get
int 0
==
bnz Finish

byte "max"
app_global_get
txn ApplicationArgs 0
btoi
<
bnz Finish

byte "heap"
app_global_get
txn ApplicationArgs 0
btoi
<
bnz Finish

//flip turn
int 1
byte "turn"
app_global_get
-
store 0
byte "turn"
load 0
app_global_put

// modify heap
byte "heap"
dup
app_global_get
txn ApplicationArgs 0
btoi
-
dup
store 0
app_global_put



// load return value as approval
Finish:
int 1
return

//initial values for heap, turn, and max
SShandle_optin:
byte "turn"
int 0
app_global_put
byte "heap"
int 12
app_global_put
byte "max"
int 3
app_global_put

handle_optin:
// Handle OptIn
// approval
int 1
return

handle_closeout:
// Handle CloseOut
//approval
int 1
return

handle_deleteapp:
// Check for creator
addr JNXSVKOM24BQWKFWOZVWNL24ONVQ5K7TP7YD6EJGU4JERFH5JMO6X35KKI
txn Sender
==
return

handle_updateapp:
// Check for creator
addr JNXSVKOM24BQWKFWOZVWNL24ONVQ5K7TP7YD6EJGU4JERFH5JMO6X35KKI
txn Sender
==
return
